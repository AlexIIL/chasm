plugins {
    id 'java'
    id 'antlr'
    id 'checkstyle'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion "9.1"
    ignoreFailures(false)
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

sourceSets {
    testData {
        java {
            srcDirs file("testData/src")
            compileClasspath = sourceSets.main.output + configurations.testCompileClasspath
            runtimeClasspath = sourceSets.main.output + project.configurations.testRuntimeClasspath
        }
    }
}

compileTestDataJava {
    destinationDir = file("testData/classes")
    sourceCompatibility = 17
    targetCompatibility = 17
}

testClasses.dependsOn("testDataClasses")

dependencies {
    implementation 'org.ow2.asm:asm:9.2'
    antlr 'org.antlr:antlr4:4.9.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.ow2.asm:asm-util:9.2'
    testImplementation 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    testImplementation 'org.reflections:reflections:0.10.2'
}

test {
    useJUnitPlatform()
}
